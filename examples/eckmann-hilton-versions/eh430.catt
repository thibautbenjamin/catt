let 1paddedL (x : *) (y : *)
    	     (f : x -> x) (g : x -> y)
	     = comp f g

coh 2padL (x(f)y) : f -> 1paddedL (id x) f

let 1paddedU (x : *)
    	     (f : x -> x) (g : x -> x)
    	     = comp f g

coh 2padU (x) : id x -> 1paddedU (id x) (id x)

coh bias (x) : 2padU x -> 2padL (id x)

coh Ibias (x) : I (2padU x) -> I (2padL (id x))

let 2paddedL (x : *) (y : *)
    	     (f : x -> y) (g : x -> y)
	     (a : id x -> id x) (b : f -> g)
	     = comp (2padL f) (1paddedL [a] [b]) (I (2padL g))

coh 3padL (x(f(a)g)y) : a -> 2paddedL (id (id x)) a

let 2paddedU (x : *)
    	     (a : id x -> id x) (b : id x -> id x)
    	     = comp (2padU x) (1paddedU [a] [b]) (I (2padU x))

coh 3padU (x) : id (id x) -> 2paddedU (id (id x)) (id (id x))

let 3paddedL (x : *) (y : *)
    	     (f : x -> y) (g : x -> y)
	     (a : f -> g) (b : f -> g)
	     (p : id (id x) -> id (id x)) (q : a -> b)
	     = comp (3padL a) (2paddedL [p] [q]) (I (3padL b))
	     
coh 4padL (x(f(a(p)b)g)y) : p -> 3paddedL (id (id (id x))) p


let 3paddedU (x : *)
    	     (p : id (id x) -> id (id x)) (q : (id (id x)) -> (id (id x)))
    	     = comp (3padU x) (2paddedU [p] [q]) (I (3padU x))

coh 4padU (x) : id (id (id x)) -> 3paddedU (id (id (id x))) (id (id (id x)))

let 4paddedL (x : *) (y : *)
    	     (f : x -> y) (g : x -> y)
	     (a : f -> g) (b : f -> g)
	     (p : a -> b) (q : a -> b)
	     (m : id (id (id x)) -> id (id (id x))) (n : p -> q)
	     = comp (4padL p) (3paddedL [m] [n]) (I (4padL q))

coh 5padL (x(f(a(p(m)q)b)g)y) : m -> 4paddedL (id (id (id (id x)))) m

let 4paddedU (x : *)
    	     (m : id (id (id x)) -> id (id (id x))) (n : id (id (id x)) -> id (id (id x)))
	     = comp (4padU x) (3paddedU [m] [n]) (I (4padU x))

coh 5padU (x) : id (id (id (id x))) -> 4paddedU (id (id (id (id x)))) (id (id (id (id x))))


let 5paddedL (x : *) (y : *)
    	     (f : x -> y) (g : x -> y)
	     (a : f -> g) (b : f -> g)
	     (p : a -> b) (q : a -> b)
	     (m : p -> q) (n : p -> q)
	     (A : (id (id (id (id x)))) -> id (id (id (id x)))) (B : m -> n)
	     = comp (5padL m) (4paddedL [A] [B]) (I (5padL n))
coh 6padL (x(f(a(p(m(A)n)q)b)g)y) : A -> 5paddedL (id (id (id (id (id x))))) A

let 5paddedU (x : *)
    	     (A : (id (id (id (id x)))) -> id (id (id (id x)))) (B : (id (id (id (id x)))) -> id (id (id (id x))))
	     = comp (5padU x) (4paddedU [A] [B]) (I (5padU x))

coh 6padU (x) : id (id (id (id (id x)))) -> 5paddedU (id (id (id (id (id x))))) (id (id (id (id (id x)))))



coh factor3padL (x) : 3padL (id (id x)) -> comp (3padU x) (comp [bias x] (comp [id (id x)] [id (id x)]) [Ibias x])

coh unfactor3padU (x) : comp (comp [bias x] (comp [id (id x)] [id (id x)]) [Ibias x])
    		      	     (I (3padL (id (id x))))
			     -> I (3padU x)

coh burger (x(f(a)g)y(h)z(j(b)k)w) : comp f h j -> comp g h k

let nat-of-id-WRT-bias (x : *) (y : *) (z : *) (w : *)
    		       (f1 : x -> y) (f2 : x -> y) (g1 : y -> z) (g2 : y -> z) (h1 : z -> w) (h2 : z -> w)
		       (a : f1 -> f2) (b : g1 -> g2) (c : h1 -> h2) 
		       = burger a [b] c





coh focus-first2-to-middle2 (x0(f1)x1(f2)x2(f3)x3(f4)x4) : comp (comp f1 f2) f3 f4 -> comp f1 (comp f2 f3) f4

coh focus-middle2-to-last2 (x0(f1)x1(f2)x2(f3)x3(f4)x4) : comp f1 (comp f2 f3) f4 -> comp f1 f2 (comp f3 f4)

let trapeziumcomposite (B0 : *) (B1 : *) (B2 : *) (B3 : *) (T0 : *) (T1 : *)
    		       (b1 : B0 -> B1) (b2 : B1 -> B2) (b3 : B2 -> B3) (sL : B0 -> T0) (sR : T1 -> B3) (m1 : T0 -> B1) (m2 : T1 -> B2) (t : T0 -> T1)
		       (fL : b1 -> comp sL m1) (fM : comp m1 b2 -> comp t m2) (fR : comp m2 b3 -> sR)
		       = comp
		       (comp (comp [fL] b2 b3)
		       (focus-first2-to-middle2 sL m1 b2 b3))
		       (comp sL [fM] b3)
		       (comp (focus-middle2-to-last2 sL t m2 b3)
		       (comp sL t [fR]))




let 3paddedLto3paddedU (x : *) (p : id (id x) -> id (id x)) (q : id (id x) -> id (id x))
    	       : 3paddedL p q -> 3paddedU p q
    	       = trapeziumcomposite (factor3padL x) (nat-of-id-WRT-bias (bias x) (comp [[p]] [[q]]) (Ibias x)) (unfactor3padU x)

let 3paddedLto3paddedU_nat  (x : *) (p0 : id (id x) -> id (id x)) (p1 : id (id x) -> id (id x)) (m : p0 -> p1) (q0 : id (id x) -> id (id x)) (q1 : id (id x) -> id (id x)) (n : q0 -> q1) = I (3paddedLto3paddedU [m] [n])

let 3paddedUto3paddedR (x : *) (p : id (id x) -> id (id x)) (q : id (id x) -> id (id x))
    		       : 3paddedU p q -> (op { 1 } (3paddedL q p))
    		       = op { 1 } (I (3paddedLto3paddedU q p))

let 3paddedUto3paddedR_nat  (x : *) (p0 : id (id x) -> id (id x)) (p1 : id (id x) -> id (id x)) (m : p0 -> p1) (q0 : id (id x) -> id (id x)) (q1 : id (id x) -> id (id x)) (n : q0 -> q1) = 3paddedUto3paddedR [m] [n]





let middlehalf (x : *)
    	       = 3paddedLto3paddedU (id (id (id x))) (id (id (id x)))

let middle (x : *) = comp (middlehalf x) (op { 1 } (I (middlehalf x)))

coh middlereplace (x) : comp (I (4padL (id (id (id x))))) (op { 1 } (4padL (id (id (id x))))) -> middle x







coh whiskering_interchange (x(f)y(g(a)h(b)i)z(k)w) : comp (comp f [a] k) (comp f [b] k) -> comp f [comp a b] k

coh whiskering_interchange+ (x(f)y(g(a(p)b(q)c)h)z(k)w) : comp (comp f [[p]] k) (comp f [[q]] k) -> comp f [[comp p q]] k

let interchange1 (x : *) (m : id (id (id x)) -> id (id (id x)))  (n : id (id (id x)) -> id (id (id x)))
    		 = whiskering_interchange (3padU x) (2paddedU [[id (id (id (id x)))]] [[m]]) (2paddedU [[n]] [[id (id (id (id x)))]]) (I (3padU x))

let interchange2 (x : *) (m : id (id (id x)) -> id (id (id x)))  (n : id (id (id x)) -> id (id (id x)))
    		 = comp (3padU x) [[whiskering_interchange+ (2padU x) (comp [[[id (id (id (id x)))]]] [[[m]]]) (comp [[[n]]] [[[id (id (id (id x)))]]]) (I (2padU x))]] (I (3padU x))

coh interchange_inside (x(f(a(p(A)q)b)g)y(h(c(r(B)s)d)i)z) : comp (comp [[[id p]]] [[[B]]]) (comp [[[A]]] [[[id s]]]) -> comp [[[A]]] [[[B]]]

let interchange3 (x : *) (m : id (id (id x)) -> id (id (id x)))  (n : id (id (id x)) -> id (id (id x)))
    		 = comp (3padU x) [[comp (2padU x) [[[interchange_inside n m]]] (I (2padU x))]] (I (3padU x))

let interchange (x : *) (m : id (id (id x)) -> id (id (id x))) (n : id (id (id x)) -> id (id (id x)))
    		 = comp (interchange1 m n) (interchange2 m n) (interchange3 m n)





coh tidyl (x) : comp (4padL (id (id (id x)))) (3paddedLto3paddedU (id (id (id x))) (id (id (id x)))) -> 4padU x

coh tidyr (x) : comp (3paddedUto3paddedR (id (id (id x))) (id (id (id x)))) (I (op { 1 } (4padL (id (id (id x)))))) -> I (4padU x)





coh focus-33-to-middle2 (x0(f1)x1(f2)x2(f3)x3(f4)x4(f5)x5(f6)x6) :
    comp (comp f1 f2 f3) (comp f4 f5 f6) -> comp f1 f2 (comp f3 f4) f5 f6

coh focus-middle2-to-1221 (x0(f1)x1(f2)x2(f3)x3(f4)x4(f5)x5(f6)x6) : comp f1 f2 (comp f3 f4) f5 f6 -> comp f1 (comp f2 f3) (comp f4 f5) f6

coh focus-1221-to-222 (x0(f1)x1(f2)x2(f3)x3(f4)x4(f5)x5(f6)x6) : comp f1 (comp f2 f3) (comp f4 f5) f6 -> comp (comp f1 f2) (comp f3 f4) (comp f5 f6)

let totalshape (A0 : *) (B0 : *) (C0 : *)
    	       	     	(B1 : *) 
    	       (A1 : *)	      	 (C1 : *)
			(B2 : *)
	       (A2 : *) (B3 : *) (C2 : *)

	       (m : A0 -> A1) (n : A1 -> A2)
	       (lpad : A0 -> B0) (lwhiskm : B0 -> B1) (ilpad : B1 -> A1) (rpad : A1 -> B2) (rwhiskn : B2 -> B3) (irpad : B3 -> A2)
	       (lmoves_outer : B0 -> C0) (lmoves_inner : B1 -> C1) (rmoves_inner : C1 -> B2) (rmoves_outer : C2 -> B3)
	       (uwhiskm : C0 -> C1) (uwhiskn : C1 -> C2) (mn : C0 -> C2) (upad : A0 -> C0) (iupad : C2 -> A2)

	       (lpadm : m -> comp lpad lwhiskm ilpad) (rpadn : n -> comp rpad rwhiskn irpad)
	       (middlereplace : comp ilpad rpad -> comp lmoves_inner rmoves_inner)
	       (lnat : comp lwhiskm lmoves_inner -> comp lmoves_outer uwhiskm)
	       (rnat : comp rmoves_inner rwhiskn -> comp uwhiskn rmoves_outer)
	       (interchange : comp uwhiskm uwhiskn -> mn) (tidyl : comp lpad lmoves_outer -> upad) (tidyr : comp rmoves_outer irpad -> iupad)

	       = comp
	       (comp [lpadm] [rpadn])
	       (focus-33-to-middle2 _ _ _ _ _ _)
	       (comp _ _ [middlereplace] _ _)
	       (focus-middle2-to-1221 _ _ _ _ _ _)
	       (comp _ [lnat] [rnat] _)
	       (focus-1221-to-222 _ _ _ _ _ _)
	       (comp [tidyl] [interchange] [tidyr])



let eh430 (x : *) (m : id (id (id x)) -> id (id (id x))) (n : id (id (id x)) -> id (id (id x)))
    	  : comp m n -> 4paddedU n m
    	  = totalshape
	  (5padL m) (op { 1 } (5padL n))
	  (middlereplace x)
	  (3paddedLto3paddedU_nat (id (id (id (id x)))) m)
	  (3paddedUto3paddedR_nat n (id (id (id (id x)))))
	  (interchange m n)
	  (tidyl x) (tidyr x)

coh factor4padL (x) : 4padL (id (id (id x))) -> comp (4padU x) (I (3paddedLto3paddedU (id (id (id x))) (id (id (id x)))))

let 3paddedUto3paddedL (x : *) (a : id (id x) -> id (id x)) (b : id (id x) -> id (id x)) = I (3paddedLto3paddedU a b)

let 3paddedUto3paddedL_nat (x : *) (a0 : id (id x) -> id (id x)) (a1 : id (id x) -> id (id x)) (a : a0 -> a1) (b0 : id (id x) -> id (id x)) (b1 : id (id x) -> id (id x)) (b : b0 -> b1) = 3paddedUto3paddedL [a] [b]

coh unfactor4padU (x) : comp (3paddedUto3paddedL (id (id (id x))) (id (id (id x)))) (I (4padL (id (id (id x))))) -> I (4padU x) 

let 4paddedLto4paddedU (x : *) (a : (id (id (id x))) -> (id (id (id x))))
    : 4paddedL (id (id (id (id x)))) a -> 4paddedU (id (id (id (id x)))) a
    = trapeziumcomposite (factor4padL x) (3paddedUto3paddedL_nat (id (id (id (id x)))) a) (unfactor4padU x)

let 4paddedLto4paddedU_nat (x : *) (a0 : (id (id (id x))) -> (id (id (id x)))) (a1 : (id (id (id x))) -> (id (id (id x)))) (a : a0 -> a1)

    = 4paddedLto4paddedU [a]


let 4paddedUto4paddedR (x : *) (a : (id (id (id x))) -> (id (id (id x))))
    = I (op { 1 } (4paddedLto4paddedU a))

let 4paddedUto4paddedR_nat (x : *) (a0 : (id (id (id x))) -> (id (id (id x)))) (a1 : (id (id (id x))) -> (id (id (id x)))) (a : a0 -> a1)

    = 4paddedUto4paddedR [a]

coh middlereplace+ (x) : comp (I (5padL (id (id (id (id x)))))) (op { 1 } (5padL (id (id (id (id x)))))) -> comp (4paddedLto4paddedU (id (id (id (id x))))) (4paddedUto4paddedR (id (id (id (id x)))))


coh whiskering_interchange++ (x(f)y(g(a(p(A)q(B)r)b)h)z(k)w) : comp (comp f [[[A]]] k) (comp f [[[B]]] k) -> comp f [[[comp A B]]] k

let interchange+1 (x : *) (a : id (id (id (id x))) -> id (id (id (id x))))  (b : id (id (id (id x))) -> id (id (id (id x))))
    = whiskering_interchange (4padU x) (3paddedU [[id (id (id (id (id x))))]] [[a]]) (3paddedU [[b]] [[id (id (id (id (id x))))]]) (I (4padU x))


let interchange+2 (x : *) (a : id (id (id (id x))) -> id (id (id (id x))))  (b : id (id (id (id x))) -> id (id (id (id x))))
    = comp (4padU x) [[whiskering_interchange+ (3padU x) (2paddedU [[[id (id (id (id (id x))))]]] [[[a]]]) (2paddedU [[[b]]] [[[id (id (id (id (id x))))]]]) (I (3padU x))]] (I (4padU x))

let interchange+3 (x : *) (a : id (id (id (id x))) -> id (id (id (id x))))  (b : id (id (id (id x))) -> id (id (id (id x))))
    = comp (4padU x) [[comp (3padU x) [[[whiskering_interchange++ (2padU x) (1paddedU [[[[id (id (id (id (id x))))]]]] [[[[a]]]]) (1paddedU [[[[b]]]] [[[[id (id (id (id (id x))))]]]]) (I (2padU x))]]] (I (3padU x))]] (I (4padU x))

coh interchange_inside+ (x(f(a(p(A(S)B)q)b)g)y(h(c(r(C(T)D)s)d)i)z) : comp (comp [[[[id A]]]] [[[[T]]]]) (comp [[[[S]]]] [[[[id D]]]]) -> comp [[[[S]]]] [[[[T]]]]

let interchange+ (x : *) (a : id (id (id (id x))) -> id (id (id (id x))))  (b : id (id (id (id x))) -> id (id (id (id x))))
 = comp
 (interchange+1 a b)
 (interchange+2 a b)
 (interchange+3 a b)
 (comp (4padU x) [[comp (3padU x) [[[comp (2padU x) [[[[interchange_inside+ b a]]]] (I (2padU x))]]] (I (3padU x))]] (I (4padU x)))

coh tidyl+ (x) : comp (5padL (id (id (id (id x))))) (4paddedLto4paddedU (id (id (id (id x))))) -> 5padU x

coh tidyr+ (x) : comp (4paddedUto4paddedR (id (id (id (id x))))) (I (op { 1 } (5padL (id (id (id (id x))))))) -> I (5padU x)

let eh540 (x : *) (a : id (id (id (id x))) -> id (id (id (id x)))) (b : id (id (id (id x))) -> id (id (id (id x))))
    	  : comp a b -> 5paddedU b a
    	  = totalshape
	  (6padL a) (op { 1 } (6padL b))
	  (middlereplace+ x)
	  (4paddedLto4paddedU_nat a)
	  (4paddedUto4paddedR_nat b)
	  (interchange+ a b)
	  (tidyl+ x) (tidyr+ x)
